/*
 TODO: Try it https://github.com/beryx-gist/badass-runtime-example-kotlin-tornadofx!!!
 */
buildscript {
    apply from: 'versions.gradle'

    repositories {
        mavenLocal()
        mavenCentral()
    }
    dependencies {
        classpath deps.kotlin.plugin
        classpath deps.junit.plugin
    }
}

plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'org.beryx.runtime' version '1.12.1'
}

apply plugin: "kotlin"
apply plugin: "kotlin-kapt"
apply plugin: "org.junit.platform.gradle.plugin"

javafx {
    version = "15.0.1"
    modules = ['javafx.controls', 'javafx.graphics', 'javafx.swing']
}

application {
    mainClassName = "io.usoamic.wallet.UsoamicWallet"
    applicationName = 'UsoamicWallet'
}

compileKotlin {
    kotlinOptions.jvmTarget = "15"
}

allprojects {
    group = "io.usoamic"
    version = "1.0.0"
}

repositories {
    jcenter()
    mavenLocal()
    mavenCentral()

    maven { url 'https://jitpack.io' }
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation deps.kotlin.stdlib

    implementation deps.gson
    implementation deps.zxing

    implementation(deps.web3j) {
        force = true
    }

    implementation deps.rx.java
    implementation deps.rx.kotlin
    implementation deps.rx.javafx

    implementation deps.usoamic.kt
    implementation deps.usoamic.validateutilkt

    implementation deps.tornado.fx

    implementation deps.fontawesomefx.commons
    implementation deps.fontawesomefx.controls
    implementation deps.fontawesomefx.fontawesome
    implementation deps.fontawesomefx.icons525
    implementation deps.fontawesomefx.materialicons
    implementation deps.fontawesomefx.materialdesignfont

    implementation deps.dagger.runtime
    kapt deps.dagger.compiler

    annotationProcessor deps.dagger.compiler

    testImplementation deps.junit.jupiter_api
    testImplementation deps.junit.jupiter_engine
}

// https://github.com/beryx-gist/badass-runtime-example-javafx
runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    modules = ['java.desktop', 'jdk.unsupported', 'java.scripting', 'java.logging', 'java.xml']

    launcher {
        noConsole = true
    }

    jpackage {
        def currentOs = org.gradle.internal.os.OperatingSystem.current()
        def imgType = currentOs.windows ? 'ico' : currentOs.macOsX ? 'icns' : 'png'

        imageOptions += ['--icon', "src/main/resources/images/ic_usoamic.$imgType"]
        installerOptions += ['--resource-dir', "src/main/resources"]
        installerOptions += ['--vendor', 'Usoamic Project']

        if (currentOs.windows) {
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
        } else if (currentOs.linux) {
            installerOptions += ['--linux-package-name', 'packageName', '--linux-shortcut']
        } else if (currentOs.macOsX) {
            installerOptions += ['--mac-package-name', 'packageName']
        }
    }
}
/*
jar {
    manifest {
        attributes(
                "Class-Path": configurations.compile.collect { it.getName() }.join(" "),
                "Main-Class": mainClassName
        )
    }
    from(configurations.compile.collect { entry -> zipTree(entry) }) {
        exclude "META-INF/MANIFEST.MF"
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
}

 */